// -------------------------------------
// Override Script
// -------------------------------------

// Joining dialogue
IF
     Global("LK#XulayeJoined","GLOBAL",0)
     Global("LK#XulJoinDialogue","GLOBAL",1)
     !Range("Solaufein",15)
THEN
    RESPONSE #100
     SetGlobal("LK#XulJoinDialogue","GLOBAL",2)
     MoveToObject(Player1)
     StartDialogueNoSet(Player1)
END


// Dialogues
IF
    InParty(Myself)
    Global("LK#XulDialogues","GLOBAL",0)
THEN
  RESPONSE #100
    RealSetGlobalTimer("LK#XulDialoguesTimer","GLOBAL",1000)
    SetGlobal("LK#XulDialogues","GLOBAL",1)
END

IF
    InParty(Myself)
    See(Player1)
    !StateCheck("Xulaye",CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    GlobalGT("udDrowPlot","GLOBAL",4)
    RealGlobalTimerExpired("LK#XulDialoguesTimer","GLOBAL")
    CombatCounter(0)
    !See([ENEMY])
    OR(4)
      Global("LK#XulDialogues","GLOBAL",1)
      Global("LK#XulDialogues","GLOBAL",3)
      Global("LK#XulDialogues","GLOBAL",5)
      Global("LK#XulDialogues","GLOBAL",7)
THEN
  RESPONSE #100
    MoveToObject(Player1)
    Dialogue(Player1)
END

IF
    InParty(Myself)
    See(Player1)
    RealGlobalTimerExpired("LK#XulDialoguesTimer","GLOBAL")
    OR(3)
      Global("LK#XulDialogues","GLOBAL",2)
      Global("LK#XulDialogues","GLOBAL",4)
      Global("LK#XulDialogues","GLOBAL",6)
THEN
  RESPONSE #100
    IncrementGlobal("LK#XulDialogues","GLOBAL",1)
END


// First quest dialogue
IF
    InParty(Myself)
    See(Player1)
    AreaType(CITY)
//    CombatCounter(0)
    !StateCheck("Xulaye",CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
//    !See([ENEMY])
    Global("LK#XulQuest","GLOBAL",1)
    RealGlobalTimerExpired("LK#XulQuestStart","GLOBAL")
THEN
  RESPONSE #100
    SetGlobal("LK#XulQuest","GLOBAL",2)
    CreateCreatureObjectOffScreen("lk#xmes",Myself,0,0,0)
END


// If Drow city is Hostile and is in party, leave party and escape area
IF
     InParty(Myself)
     Global("DrowCityHostile","GLOBAL",1)
     !GlobalGT("udDrowPlot","GLOBAL",30)
THEN
  RESPONSE #100
     SetGlobal("LK#XulLeftAngry","GLOBAL",1)
     SetGlobal("LK#XulayeJoined","GLOBAL",0)
     LeaveParty()
     EscapeArea()
END


// Setting the "Xulaye knows PC is a surfacer" variable
IF
    InParty(Myself)
    Global("LK#XulayeKnows","GLOBAL",0)
    GlobalGT("LK#XulDialogues","GLOBAL",5)
THEN
  RESPONSE #100
    SetGlobal("LK#XulayeKnows","GLOBAL",1)
END


// Setting talk variables if Xulaye finds out before the 3rd talk
IF
    InParty(Myself)
    Global("LK#XulayeKnows","GLOBAL",0)
    OR(2)
      Global("DrowSlavesFree","GLOBAL",1)
      Global("JarlaxleHQ","GLOBAL",1)
THEN
  RESPONSE #100
    SetGlobal("LK#XulayeKnows","GLOBAL",1)
    SetGlobal("LK#XulDialogues","GLOBAL",4)
    RealSetGlobalTimer("LK#XulDialoguesTimer","GLOBAL",30)
END


// Aboleth scenery talk
IF
    InParty(Myself)
    See(Player1)
    !StateCheck("Xulaye",CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    CombatCounter(0)
    !See([ENEMY])
    Global("TalkedToAboleth","GLOBAL",1) 
    !Global("RefusedQilueBrain","GLOBAL",1)
    Global("LK#XulAboleth","GLOBAL",0)
THEN
  RESPONSE #100
    SetGlobal("LK#XulAboleth","GLOBAL",1)
END

IF
    InParty(Myself)
    See(Player1)
    !StateCheck("Xulaye",CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    CombatCounter(0)
    !See([ENEMY])
    Global("LK#XulAboleth","GLOBAL",1)
THEN
  RESPONSE #100
    MoveToObject(Player1)
    Dialogue(Player1)
END


// Quest part 2; if Sola is dead, Xulaye gives eggs
IF
    InParty(Myself)
    See(Player1)
    Global("LK#XulGivesEggs","GLOBAL",1)
THEN
  RESPONSE #100
    MoveToObject(Player1)
    Dialogue(Player1)
END


// No killing Solaufein!
IF
    InParty(Myself)
    See(Player1)
    !StateCheck("Xulaye",CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    CombatCounter(0)
    !See([ENEMY])
    Global("SolaufeinJob","GLOBAL",8)
    AreaCheck("AR2200")
    Global("LK#XulNoKillingSola","GLOBAL",0)
THEN
  RESPONSE #100
    SetGlobal("LK#XulNoKillingSola","GLOBAL",1)
END

IF
    InParty(Myself)
    See(Player1)
    !StateCheck("Xulaye",CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    CombatCounter(0)
    !See([ENEMY])
    Global("LK#XulNoKillingSola","GLOBAL",1)
THEN
  RESPONSE #100
    MoveToObject(Player1)
    Dialogue(Player1)
END


// If you gave Phaere the real eggs instead of the fake ones
IF
    InParty(Myself)
    See(Player1)
    !StateCheck("Xulaye",CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    CombatCounter(0)
    !See([ENEMY])
    Global("PhaereJob","GLOBAL",11)
    Global("LK#XulGaveRealEggs","GLOBAL",0)
    !PartyHasItem("MISC9T")
    OR(2)
      PartyHasItem("LK#XEGGS")
      PartyHasItem("MISC9V")
THEN
  RESPONSE #100
    SetGlobal("LK#XulGaveRealEggs","GLOBAL",1)
END

IF
    InParty(Myself)
    See(Player1)
    !StateCheck("Xulaye",CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    CombatCounter(0)
    !See([ENEMY])
    Global("LK#XulGaveRealEggs","GLOBAL",1)
THEN
  RESPONSE #100
    MoveToObject(Player1)
    Dialogue(Player1)
END


// Left angry scene... scary scary.
IF
    Global("LK#XulayeLeftAngry","GLOBAL",2)
    Range([PC],15)
THEN
  RESPONSE #100
    StartDialogueNoSet(Player1)
END


// Quest reminder
IF  
    GlobalTimerExpired("LK#XulQuest1Timer","GLOBAL")
    InParty(Myself)
    See(Player1)
    !StateCheck("Xulaye",CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    CombatCounter(0)
    !See([ENEMY])
    !PartyHasItem("lk#xarm")
    Global("LK#XulQuestReminder","GLOBAL",0)
    GlobalLT("LK#XULQUEST","GLOBAL",7)
THEN
  RESPONSE #100
    SetGlobal("LK#XulQuestReminder","GLOBAL",1)
END

IF
    InParty(Myself)
    See(Player1)
    !StateCheck("Xulaye",CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    CombatCounter(0)
    !See([ENEMY])
    !PartyHasItem("lk#xarm")
    Global("LK#XulQuestReminder","GLOBAL",1)
    GlobalLT("LK#XULQUEST","GLOBAL",7)
THEN
  RESPONSE #100
    MoveToObject(Player1)
    Dialogue(Player1)
END

IF
    GlobalTimerExpired("LK#XulQuest1Timer2","GLOBAL")
    InParty(Myself)
    See(Player1)
    !StateCheck("Xulaye",CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    CombatCounter(0)
    !See([ENEMY])                       
    !PartyHasItem("lk#xarm")
    Global("LK#XulQuestReminder","GLOBAL",2)
    GlobalLT("LK#XULQUEST","GLOBAL",7)
THEN
  RESPONSE #100
    IncrementGlobal("LK#XulQuestReminder","GLOBAL",1)
END

IF
    InParty(Myself)
    See(Player1)
    !StateCheck("Xulaye",CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    CombatCounter(0)
    !See([ENEMY])
    !PartyHasItem("lk#xarm")
    Global("LK#XulQuestReminder","GLOBAL",3)
    GlobalLT("LK#XULQUEST","GLOBAL",7)
THEN
  RESPONSE #100
    MoveToObject(Player1)
    Dialogue(Player1)
END


// Reaction to being captured by Illithid
IF
    InParty(Myself)
    AreaCheck("AR2400")
    Global("LK#XulIllithid","GLOBAL",0)
THEN
  RESPONSE #100
    RealSetGlobalTimer("LK#XulIllithidTimer","GLOBAL",60)
    SetGlobal("LK#XulIllithid","GLOBAL",1)
END

IF
    InParty(Myself)
    See(Player1)
    RealGlobalTimerExpired("LK#XulIllithidTimer","GLOBAL")
    Global("LK#XulIllithid","GLOBAL",1)
THEN
  RESPONSE #100
    IncrementGlobal("LK#XulIllithid","GLOBAL",1)
END

IF
    InParty(Myself)
    See(Player1)
    !StateCheck("Xulaye",CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    RealGlobalTimerExpired("LK#XulIllithidTimer","GLOBAL")
    Global("LK#XulIllithid","GLOBAL",2)
THEN
  RESPONSE #100
    MoveToObject(Player1)
    Dialogue(Player1)
END


// Reaction to being in the Beholder lair
IF
    InParty(Myself)
    See(Player1)
    !StateCheck("Xulaye",CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    AreaCheck("AR2101")
    Global("LK#XulBeholder","GLOBAL",0)
THEN
  RESPONSE #100
    SetGlobal("LK#XulBeholder","GLOBAL",1)
END

IF
    InParty(Myself)
    See(Player1)
    !StateCheck("Xulaye",CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    Global("LK#XulBeholder","GLOBAL",1)
THEN
  RESPONSE #100
    MoveToObject(Player1)
    Dialogue(Player1)
END


// Reaction to killing the Kua-Toa prince
IF
    InParty(Myself)
    See(Player1)
    !StateCheck("Xulaye",CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    CombatCounter(0)
    !See([ENEMY])
    Dead("udprince")
    AreaCheck("AR2402")
    Global("LK#XulKuatoa","GLOBAL",0)
THEN
  RESPONSE #100
    SetGlobal("LK#XulKuatoa","GLOBAL",1)
END

IF
    InParty(Myself)
    See(Player1)
    !StateCheck("Xulaye",CD_STATE_NOTVALID)
    !StateCheck(Player1,CD_STATE_NOTVALID)
    CombatCounter(0)
    !See([ENEMY])
    AreaCheck("AR2402")
    Global("LK#XulKuatoa","GLOBAL",1)
THEN
  RESPONSE #100
    MoveToObject(Player1)
    Dialogue(Player1)
END


// Fight
IF
    AttackedBy(Player1,DEFAULT)
    !Allegiance(Myself,ENEMY)
    !AreaType(CITY)
THEN
  RESPONSE #100
    ChangeAIScript("lk#xulai",OVERRIDE)
    Enemy()
END

IF
    AttackedBy(Player1,DEFAULT)
    !Allegiance(Myself,ENEMY)
    AreaType(CITY)
THEN
  RESPONSE #100
    SetGlobal("DrowCityHostile","GLOBAL",1)
    ChangeAIScript("lk#xulai",OVERRIDE)
    Enemy()
END